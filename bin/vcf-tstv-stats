#!/usr/bin/perl

#
# Copyright 2018-2023 Luciano Xumerle. All rights reserved.
# Luciano Xumerle <luciano.xumerle@gmail.com>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;

my $cmd = $0;
$cmd =~ s/^.*\///;
print STDERR
  qq{---------------------------------------------------------------------------
$cmd version 0.2.0 (May 4, 2023)
Copyright 2018-2023 by Luciano Xumerle <luciano.xumerle\@gmail.com>
This is free software; see the source for copying conditions. There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
---------------------------------------------------------------------------

SYNTAX:

 > $cmd <file1.vcf[.gz]> [file2.vcf[.gz]] ...

};

my $dest = '';

# hash -> sample -> key = sums where key == genotype ot multi or tot or indel
my $stats = {};

if ( defined $ARGV[0] && -f $ARGV[0] && $ARGV[0] =~ m/\.g*vcf/i )
{
    $dest = $ARGV[0];
    $dest =~ s/.vcf.*/.stats.tsv/i;
    if ( -f $dest )
    {
        print STDERR qq|File $dest exists!!! Nothing to do!!!\n\n|;
    }
}
else
{
    print STDERR qq|NO vcf file in input\n|;
    exit;
}

# print OUT
open( OUT, ">$dest" ) || die;
print OUT join( "\t",
    "Sample",        'variants', 'omoVar', 'het',
    'het.omo.ratio', 'snv',      'indel',  'ts.tv.ratio' ),
  "\n";

foreach my $vc (@ARGV)
{
    my @header = ();

    if ( $vc =~ m/gz$/i )
    {
        open( FILE, "gunzip -dc $vc |" ) || die;
    }
    else
    {
        open( FILE, $vc ) || die;
    }

    #
    # from here until final output is almost equal to original code
    #
    my $n            = 0;
    my $multiallelic = 0;

    while ( my $line = <FILE> )
    {
        chomp $line;

        # save the header
        if ( $line =~ m/#CHROM/ )
        {
            @header = split /\t/, $line;
            next;
        }

        # keep only the variant PASSing the filter
        next if ( $line !~ m/\tPASS\t/ );

        my @a = split /\t/, $line;

        my @alleles = split ',', $a[3] . ',' . $a[4];

        for ( my $i = 9 ; $i <= $#a ; $i++ )
        {
            my $gt = $a[$i];
            $gt =~ s/:.*$//;

            # skip BGI lines with GT=number/.
            next if ( $gt =~ m/\./ );

            # correct strelka GT
            $gt = $gt . '/' . $gt if ( length($gt) == 1 );

            # next is gt is is not a variant
            if ( $gt =~ m/[123456789]+/ )
            {
                my @gg = sort { $a <=> $b } split /[\/\|]/, $gt;
                my $geno = $alleles[0] . $alleles[ $gg[1] ];
                $$stats{ $header[$i] }->{'tot'}++;
                if   ( $gg[0] == $gg[1] ) { $$stats{ $header[$i] }->{'omo'}++; }
                else                      { $$stats{ $header[$i] }->{'het'}++; }

                next if ( length($geno) > 2 );

                $$stats{ $header[$i] }->{'multi'}++ if ( $a[4] =~ m/,/ );
                $$stats{ $header[$i] }->{$geno}++;
            }
        }
    }
    close FILE;

    for ( my $i = 9 ; $i <= $#header ; $i++ )
    {
        my $ts = 0;
        foreach my $mut ( 'AG', 'GA', 'CT', 'TC' )
        {
            $ts += $$stats{ $header[$i] }->{$mut}
              if ( defined $$stats{ $header[$i] }->{$mut} );
        }

        my $tv = 0;
        foreach my $mut ( 'AC', 'CA', 'GT', 'TG', 'AT', 'TA', 'CG', 'GC' )
        {
            $tv += $$stats{ $header[$i] }->{$mut}
              if ( defined $$stats{ $header[$i] }->{$mut} );
        }
        my $ratio = $tv ? $ts / $tv : 0;

        my $n   = $$stats{ $header[$i] }->{'tot'};
        my $omo = 0.00000001;
        $omo = $$stats{ $header[$i] }->{'omo'}
          if ( defined $$stats{ $header[$i] }->{'omo'} );
        my $het      = $$stats{ $header[$i] }->{'het'};
        my $hetVSomo = $het / $omo;
        printf OUT $header[$i] . qq|\t%d\t%d\t%d\t%.2f\t%d\t%d\t%.2f\n|, $n,
          $omo, $het, $hetVSomo, $ts + $tv,
          $n - $ts - $tv, $ratio;
    }
}
close OUT;

print STDERR qq|Stats saved on file $dest\n\n|;
